buildscript {
    ext {
        jooqVersion = '3.11.10'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10")
        classpath('org.postgresql:postgresql:42.2.4')
    }
}

plugins {
    id "nu.studer.jooq" version "3.0.3"
    id "org.flywaydb.flyway" version "8.4.4"
    id "com.gorylenko.gradle-git-properties" version "2.0.0"
    id "org.springframework.boot" version "2.5.9"
    id "org.jetbrains.kotlin.plugin.spring" version "1.6.10"
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'

group = 'poc.jooq'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

ext {
    datasource_url = "jdbc:postgresql://localhost:5432/xpatrio?characterEncoding=UTF-8"
    datasource_username = "xpatrio"
    datasource_password = "xpatrio"
}


mainClassName = 'poc.jooq.AppKt'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'


    implementation "commons-io:commons-io:2.6"

    implementation "org.jooq:jooq:$jooqVersion"
    implementation 'org.postgresql:postgresql:42.2.4'

    jooqRuntime 'org.postgresql:postgresql:42.2.4'
    jooqRuntime "org.jooq:jooq-codegen:$jooqVersion"
    jooqRuntime "org.jooq:jooq-meta:$jooqVersion"
    jooqRuntime "org.jooq:jooq:$jooqVersion"

    implementation 'org.flywaydb:flyway-core:8.4.4'

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '1.8'
        useIR = true
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '1.8'
        useIR = true
    }
}


sourceSets {
    generated {
        java {
            srcDirs = ['src/generated/java']
        }
        compileClasspath += sourceSets.main.runtimeClasspath
    }
    main {
        java {
            srcDirs += ['src/generated/java']
        }
    }
}



clean {
    delete sourceSets.generated.java.srcDirs
}

idea {
    module {
        sourceDirs += file('src/generated/java')
    }
}


flyway {
    url = "${datasource_url}"
    user = "${datasource_username}"
    password = "${datasource_password}"
    locations = ['filesystem:src/main/resources/db/migration']
    table = 'schema_version'
    baselineOnMigrate = true
    ignoreMissingMigrations = true
    outOfOrder = true
    cleanDisabled = true
}

flywayMigrate.dependsOn flywayRepair

jooq {
    version = "$jooqVersion"
    poc(sourceSets.main) {
        jdbc {
            driver = "org.postgresql.Driver"
            url = "jdbc:postgresql://localhost:5432/xpatrio?characterEncoding=UTF-8"
            user = "xpatrio"
            password = "xpatrio"
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
                excludes = 'schema_version|pgp_armor_headers'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                fluentSetters = true
                daos = false
                pojos = false
                immutablePojos = false
            }
            target {
                packageName = "poc.jooq.generated.db"
                directory = "src/generated/java"
            }
        }
    }
}

bootJar {
    archiveFileName = "app.jar"
}

generatePocJooqSchemaSource.dependsOn flywayMigrate
